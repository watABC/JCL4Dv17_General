  //JCL_all_isMissmatched
  //20150412 yabe wat
  //読み込むファイル群と、テーブル構成が合っているか？

C_TEXT($1;$folder_Path)  //フォルダパスのポインタ
$folder_Path:=$1
C_POINTER($2;$aryFileListPtr)  //ファイル名配列のポインタ
$aryFileListPtr:=$2
C_POINTER($3)
C_TEXT($errMsg)
$errMsg:=""

C_LONGINT($0;$errCount)
C_LONGINT($i;$k;$index)
C_TEXT($tableName;$fieldName;$fileName)
C_TIME($FileRef)
C_TEXT($fileText)
ARRAY TEXT($lineAry;0)
ARRAY TEXT($FileFieldAry;0)


For ($i;1;Get last table number)
	
	If (Is table number valid($i)=True)
		
		  // 20150408 wat 郵便番号テーブルは除く
		If (Table name($i)#"Z_PostalCode")
			
			  //テーブルチェック
			$tableName:=Table name($i)
			$index:=Find in array($aryFileListPtr->;$tableName+".txt")
			If ($index#-1)
				
				  //テーブル名が配列内にあれば
				$fileName:=$folder_Path+$tableName+".txt"
				
				  //フィールドチェック､ファイルを開いて調べる
				$open_ok:=Jiro_file_OpenByName ($fileName;->$FileRef)
				If ($open_ok=1)
					
					$receive_ok:=Jiro_file_ReadAllUTF8 ($FileRef;->$fileText)
					If ($receive_ok=1)
						
						$numOfLines:=Jiro_str_Extract ($fileText;Char(Carriage return)+Char(Line feed);->$lineAry)
						If ($numOfLines>=1)
							
							  //「ID」という文字がファイルの先頭にあるとエクセルがSYLKファイルと勘違いするため先頭に出力していた空白を削除
							$lineAry{1}:=Replace string($lineAry{1};" ";"";1)
							$numOfItems:=Jiro_str_Extract ($lineAry{1};Char(Tab);->$FileFieldAry)
							
							For ($k;1;Get last field number($i))
								
								If (Is field number valid($i;$k)=True)
									
									$fieldName:=Field name($i;$k)
									$index:=Find in array($FileFieldAry;$fieldName)
									If ($index#-1)
										
									Else 
										
										  // ファイルの１行目に、フィールド名がない
										$errMsg:=$errMsg+$fieldName+Char(Carriage return)
										$errCount:=$errCount+1
										
									End if 
									
								End if 
								
							End for 
							
							DELETE FROM ARRAY($FileFieldAry;1;Size of array($FileFieldAry))
							
						End if 
						
						DELETE FROM ARRAY($lineAry;1;Size of array($lineAry))
						
					End if 
					
					Jiro_file_Close ($FileRef)
					
				End if 
				
			Else 
				
				$errMsg:=$errMsg+$tableName+Char(Carriage return)
				$errCount:=$errCount+1
				
			End if 
			
		End if 
		
	End if 
	
End for 

$3->:=$errMsg
$0:=$errCount
