  //JCL_Export_fromListBox
  //zz_Export
  //20140921 wat yabe
  // 汎用のリストボックス　書き出し

C_POINTER($1;$lstbxPtr)
$lstbxPtr:=$1
C_TEXT($2;$name)
$name:=$2
C_BOOLEAN($0;$done)
$done:=False
C_TEXT($buf)
$buf:=" "  // 20140923 「ID」という文字がファイルの先頭にあるとエクセルがSYLKファイルと勘違いするため先頭に空白を出力
C_TEXT($tabChar;$crChar)
$tabChar:=Char(Tab)
$crChar:=Char(Carriage return)+Char(Line feed)
$crChar:=Char(Line feed)  //20170222
C_TEXT($folderPath;$filePath)
C_LONGINT($numOfCols;$numOfRows;$col;$row)
C_LONGINT($open_ok;$dlg_ok)

$name:=$name+JCL_str_Datemark (Current date;Current time)+".txt"
$dlg_ok:=JCL_dlg_InputOne ("出力先のファイル名：";"フィル名を入力してください。";"OK";"Cancel";->$name)
If ($dlg_ok=1)
	
	  // 列数を求める
	$numOfCols:=LISTBOX Get number of columns($lstbxPtr->)
	$numOfRows:=LISTBOX Get number of rows($lstbxPtr->)
	
	ARRAY TEXT($aryColNames;0)
	ARRAY TEXT($aryHeaderNames;0)
	ARRAY POINTER($aryColVars;0)
	ARRAY POINTER($aryHeaderVers;0)
	ARRAY BOOLEAN($aryColsVisible;0)
	ARRAY POINTER($aryStyles;0)
	
	LISTBOX GET ARRAYS($lstbxPtr->;$aryColNames;$aryHeaderNames;$aryColVars;$aryHeaderVers;$aryColsVisible;$aryStyles)
	
	  // ヘッダを出力
	For ($col;1;$numOfCols)
		
		If ($col<$numOfCols)
			
			$str:=OBJECT Get title(*;$aryHeaderNames{$col})
			$buf:=$buf+$str+$tabChar
			
		Else 
			
			$str:=OBJECT Get title(*;$aryHeaderNames{$col})
			$buf:=$buf+$str+$tabChar+$crChar
			
		End if 
	End for 
	
	  // ボディを出力
	For ($row;1;$numOfRows)
		
		For ($col;1;$numOfCols)
			
			If ($col<$numOfCols)
				
				$str:=String($aryColVars{$col}->{$row})  // データ型によってはエラーになる可能性がある
				
				$buf:=$buf+$str+$tabChar
				
			Else 
				
				$str:=String($aryColVars{$col}->{$row})  // データ型によってはエラーになる可能性がある
				$buf:=$buf+$str+$crChar
				
			End if 
		End for 
	End for 
	
	$folderPath:=System folder(Desktop)
	$filePath:=JCL_file_MakeFilePath ($folderPath;$name)
	$open_ok:=JCL_file_OpenForWrite ($filePath;->$FileRef)
	If ($open_ok=1)
		
		  //ファイルに書き出す
		JCL_file_WriteSJIS ($FileRef;$buf)
		
		  //ファイルを閉じる
		CLOSE DOCUMENT($FileRef)
		
		$done:=True
		
	End if 
	
End if 

$0:=$done
