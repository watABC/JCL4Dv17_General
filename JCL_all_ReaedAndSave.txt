  //JCL_all_ReaedAndSave
  //20150412 yabe wat
  //ファイル群を読み込み、テーブルに保存

C_TEXT($1;$folder_Path)  //フォルダパスのポインタ
$folder_Path:=$1
C_POINTER($2;$aryFileListPtr)  //ファイル名配列のポインタ
$aryFileListPtr:=$2
C_POINTER($3)
C_TEXT($errMsg)
$errMsg:=""

C_LONGINT($i;$k;$index;$n)
C_TEXT($tableName;$fieldName;$fileName)
C_TIME($FileRef)
C_TEXT($fileText)
ARRAY TEXT($lineAry;0)
ARRAY TEXT($FileFieldAry;0)
ARRAY TEXT($itemAry;0)
C_LONGINT($minimumCols;$numOfLines)
C_LONGINT($open_ok;$receive_ok)

For ($i;1;Get last table number)
	
	If (Is table number valid($i)=True)
		
		  // 20150408 wat 郵便番号テーブルは除く
		If (Table name($i)#"Z_PostalCode")
			
			$minimumCols:=0
			
			$tablePrt:=Table($i)
			$tableName:=Table name($i)
			$fileName:=$folder_Path+$tableName+".txt"
			
			$minimumCols:=JCL_fld_numOfFields ($i;$k)
			
			$open_ok:=Jiro_file_OpenByName ($fileName;->$FileRef)
			
			If ($open_ok=1)
				
				$receive_ok:=Jiro_file_ReadAllUTF8 ($FileRef;->$fileText)
				
				If ($receive_ok=1)
					
					$numOfLines:=Jiro_str_Extract ($fileText;Char(Carriage return)+Char(Line feed);->$lineAry)
					
					  //「ID」という文字がファイルの先頭にあるとエクセルがSYLKファイルと勘違いするため先頭に出力していた空白を削除
					$lineAry{1}:=Replace string($lineAry{1};" ";"";1)
					$numOfItems:=Jiro_str_Extract ($lineAry{1};Char(Tab);->$FileFieldAry)
					
					For ($n;2;$numOfLines)
						
						$numOfItems:=Jiro_str_Extract ($lineAry{$n};Char(Tab);->$itemAry)
						
						If ($minimumCols<=$numOfItems)
							
							CREATE RECORD($tablePrt->)
							
							For ($k;1;Get last field number($i))
								
								If (Is field number valid($i;$k)=True)
									
									$fieldName:=Field name($i;$k)
									$fieldPtr:=Field($i;$k)
									$index:=Find in array($FileFieldAry;$fieldName)
									
									<>gZU00_Counter:=<>gZU00_Counter+1  //状態ダイアログに表示
									POST OUTSIDE CALL(-1)
									
									$type:=JCL_fld_Type ($i;$k)
									Case of 
										: ($type=Is alpha field)
											$fieldPtr->:=$itemAry{$index}
											$fieldPtr->:=Replace string($fieldPtr->;"<br><br>";Char(Carriage return)+Char(Line feed))
											$fieldPtr->:=Replace string($fieldPtr->;"<br>";Char(Carriage return))
										: ($type=Is text)
											$fieldPtr->:=$itemAry{$index}
											$fieldPtr->:=Replace string($fieldPtr->;"<br><br>";Char(Carriage return)+Char(Line feed))
											$fieldPtr->:=Replace string($fieldPtr->;"<br>";Char(Carriage return))
										: ($type=Is real)
											$fieldPtr->:=Num($itemAry{$index})
										: ($type=Is integer)
											$fieldPtr->:=Num($itemAry{$index})
										: ($type=Is longint)
											$fieldPtr->:=Num($itemAry{$index})
										: ($type=Is date)
											$fieldPtr->:=Date($itemAry{$index})
										: ($type=Is time)
											$fieldPtr->:=Time($itemAry{$index})
										: ($type=Is boolean)
											If ($itemAry{$index}="True")
												$fieldPtr->:=True
											Else 
												$fieldPtr->:=False
											End if 
										: ($type=Is picture)
											  //書き出ししない
										: ($type=Is subtable)
											  //書き出ししない
										: ($type=Is BLOB)
											  //書き出ししない
									End case 
									
								End if 
								
							End for 
							
							SAVE RECORD($tablePrt->)
							
						End if 
						
						DELETE FROM ARRAY($itemAry;1;Size of array($itemAry))
						
					End for 
					
					DELETE FROM ARRAY($FileFieldAry;1;Size of array($FileFieldAry))
					DELETE FROM ARRAY($lineAry;1;Size of array($lineAry))
					
				End if 
				
				JCL_file_Close ($FileRef)
				
			End if 
			
		End if 
		
	End if 
	
End for 
